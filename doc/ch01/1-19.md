## Упражнение 1.19

Существует хитрый алгоритм получения чисел Фибоначчи за логарифмическое число шагов. Вспомните трансформацию переменных состояния `a` и `b` процесса `fib-iter` из раздела `1.2.2`:
`a ← a + b` и `b ← a`. Назовем эту трансформацию `T` и заметим, что n-кратное применение `T` , начиная с 1 и 0, дает нам пару `Fib(n + 1)` и `Fib(n)`. Другими словами, числа Фибоначчи получаются путем применения `T^n`, n-ой степени трансформации `T`, к паре `(1,0)`. Теперь рассмотрим `T` как частный случай `p = 0`, `q = 1` в семействе трансформаций `Tpq`, где `Tpq` преобразует пару `(a, b)` по правилу `a ← bq + aq + ap`, ` b ← bp + aq`. Покажите, что двукратное применение трансформации `Tpq` равносильно однократному применению трансформации `Tp′q′` того же типа, и вычислите `p′` и `q′` через `p` и `q`. Это дает нам прямой способ возводить такие трансформации в квадрат, и таким образом, мы можем вычислить `T^n` с помощью последовательного возведения в квадрат, как в процедуре `fast-expt`. Используя все эти идеи, завершите следующую процедуру, которая дает результат за логарифмическое число шагов:
```racket
(define (fib n)
  (fib-iter 1 0 0 1 n))
(define (fib-iter a b p q count) (cond ((= count 0) b)
        ((even? count)
         (fib-iter a
                   b
                   ⟨??⟩ ; вычислить p’
                   ⟨??⟩ ; вычислить q’
                   (/ count 2)))
        (else (fib-iter (+ (* b q) (* a q) (* a p))
                        (+ (* b p) (* a q))
                        p
                        q
                        (- count 1)))))
```
## Решение

```
a ← bq + aq + ap
b ← bp + aq
```
```
a ← (bp + aq)q + (bq + aq + ap)q + (bq + aq + ap)p
b ← (bp + aq)p + (bq + aq + ap)q
```
Раскроем скобки:
```
a ← bpq + aq^2 + bq^2 + aq^2 + apq + bpq + apq + ap^2
b ← bp^2 + apq + bq^2 + aq^2 + apq
```
Сгруппируем:
```
a ← b(q^2 + 2pq) + a(p^2 + q^2) + a(q^2 + 2pq)
b ← b(p^2 + q^2) + a(q^2 + 2pq)
```
```
p′ ← p^2 + q^2
q′ ← q^2 + 2pq
```
```racket
#lang racket

(define (fib n)
  (fib-iter 1 0 0 1 n))
(define (square n)
  (* n n))

(define (fib-iter a b p q count)
  (cond ((= count 0) b)
        ((even? count)
         (fib-iter a
                   b
                   (+ (square p)
                      (square q)) ; вычислить p’
                   (+ (square q)
                      (* 2 p q)) ; вычислить q’
                   (/ count 2)))
        (else (fib-iter (+ (* b q) (* a q) (* a p))
                        (+ (* b p) (* a q))
                        p
                        q
                        (- count 1)))))
```
